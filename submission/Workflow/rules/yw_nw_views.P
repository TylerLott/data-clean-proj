
:- table nw_activation_from_yw_step/5.
nw_activation_from_yw_step(ActivationId, FunctionName, StepId, StepName, ActivationLine) :-
    nw_function_activation(ActivationId, _, FunctionName, ActivationLine, _),
    yw_workflow_step(StepId, StepName, _, _,  StepBeginLine, StepEndLine),
    line_inside_range(ActivationLine, StepBeginLine, StepEndLine).

:- table nw_activation_into_yw_program/6.
nw_activation_into_yw_program(ActivationId, FunctionName, StepId, StepName, ActivationLine, CallerActivationId) :-
    yw_program(StepId, StepName, _, _,  StepBeginLine, StepEndLine),
    nw_function_activation(ActivationId, _, FunctionName, ActivationLine, CallerActivationId),
    line_outside_range(ActivationLine, StepBeginLine, StepEndLine),
    nw_function_definition(_, FunctionName, FunctionStartLine, _, _),
    line_inside_range(FunctionStartLine, StepBeginLine, StepEndLine).

:- table nw_activation_into_yw_step_subprogram/2.
nw_activation_into_yw_step_subprogram(ActivationId, StepId) :-
    nw_activation_into_yw_program(ActivationId, _, StepId, _, _, _),
    nw_activation_into_yw_program(ActivationId, _, SubprogramId, _, _, _),
    yw_workflow_step(SubprogramId, _, StepId, _, _, _).

:- table nw_activation_into_yw_step/6.
nw_activation_into_yw_step(ActivationId, FunctionName, StepId, StepName, ActivationLine, CallerActivationId) :-
    nw_activation_into_yw_program(ActivationId, FunctionName, StepId, StepName, ActivationLine, CallerActivationId),
    not nw_activation_into_yw_step_subprogram(ActivationId, StepId).

:- table nw_variable_for_yw_in_port/10.
nw_variable_for_yw_in_port(VariableId, VariableName, VariableValue, DataId, DataName, Type, PortId, PortName, StepId, StepName) :-
    nw_variable_for_yw_in_port(VariableId, VariableName, VariableValue, DataId, DataName, PortId, PortName, StepId, StepName),
    Type = variable.
nw_variable_for_yw_in_port(VariableId, VariableName, VariableValue, DataId, DataName, Type, PortId, PortName, StepId, StepName) :-
    nw_argument_for_yw_in_port(VariableId, VariableName, VariableValue, DataId, DataName, PortId, PortName, StepId, StepName),
    Type = argument.
nw_variable_for_yw_in_port(VariableId, VariableName, VariableValue, DataId, DataName, Type, PortId, PortName, StepId, StepName) :-
    nw_variable_for_yw_inflow(VariableId, VariableName, VariableValue, DataId, DataName, PortId, PortName, StepId, StepName),
    Type = inflow.
nw_variable_for_yw_in_port(VariableId, VariableName, VariableValue, DataId, DataName, Type, PortId, PortName, StepId, StepName) :-
    nw_variable_for_yw_in_port_defined(VariableId, VariableName, VariableValue, DataId, DataName, PortId, PortName, StepId, StepName),
    Type = defined.

:- table nw_variable_for_yw_out_port/9.
nw_variable_for_yw_out_port(VariableId, VariableName, VariableValue, DataId, DataName, PortId, PortName, StepId, StepName) :-
    nw_variable_for_yw_out_port_assigned(VariableId, VariableName, VariableValue, DataId, DataName, PortId, PortName, StepId, StepName).
nw_variable_for_yw_out_port(VariableId, VariableName, VariableValue, DataId, DataName, PortId, PortName, StepId, StepName) :-
    nw_variable_for_yw_out_port_thru(VariableId, VariableName, VariableValue, DataId, DataName, PortId, PortName, StepId, StepName).
nw_variable_for_yw_out_port(VariableId, VariableName, VariableValue, DataId, DataName, PortId, PortName, StepId, StepName) :-
    nw_variable_for_yw_outflow(VariableId, VariableName, VariableValue, DataId, DataName, PortId, PortName, StepId, StepName).

:- table nw_variable_for_yw_data/5.
nw_variable_for_yw_data(VariableId, VariableName, VariableValue, DataId, DataName) :-
    nw_variable_for_yw_in_port(VariableId, VariableName, VariableValue, DataId, DataName, _, _, _, _, _).
nw_variable_for_yw_data(VariableId, VariableName, VariableValue, DataId, DataName) :-
    nw_variable_for_yw_out_port(VariableId, VariableName, VariableValue, DataId, DataName, _, _, _, _).
